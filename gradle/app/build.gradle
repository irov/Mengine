import com.Mengine.Utils

apply plugin: 'com.android.application'

apply from: rootProject.projectDir.getAbsolutePath() + '/base.gradle'
apply from: rootProject.projectDir.getAbsolutePath() + '/androidx.gradle'
apply from: rootProject.projectDir.getAbsolutePath() + '/minify.gradle'
apply from: rootProject.projectDir.getAbsolutePath() + '/split.gradle'

def ANDROID_APP_MANIFEST_CI = Utils.getBooleanProperty(project, "ANDROID_APP_MANIFEST_CI", false)
def ANDROID_APP_ASSETS_RES_CI = Utils.getBooleanProperty(project, "ANDROID_APP_ASSETS_RES_CI", false)
def ANDROID_APP_BUILD_PUBLISH = Utils.getBooleanProperty(project, "ANDROID_APP_BUILD_PUBLISH", false)
def ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE = Utils.getBooleanProperty(project, "ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE", false)
def ANDROID_APP_SPLIT_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false)
def ANDROID_APP_BUNDLE_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_BUNDLE_ENABLE", false)
def ANDROID_APP_DELIVERY_PACKAGES = Utils.getStringProperty(project, "ANDROID_APP_DELIVERY_PACKAGES", null)
def ANDROID_APP_BUILD_NUMBER = Utils.getIntegerProperty(project, "ANDROID_APP_BUILD_NUMBER", 0)
def ANDROID_APP_DEBUG_MINIFY_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_DEBUG_MINIFY_ENABLE", false)
def ANDROID_APP_RELEASE_MINIFY_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_RELEASE_MINIFY_ENABLE", false)

def MENGINE_APP_LIBRARY_MENGINE = Utils.existAppLibrary(project, "MENGINE_APP_LIBRARY_MENGINE")
def MENGINE_APP_LIBRARY_OPENAL32 = Utils.existAppLibrary(project, "MENGINE_APP_LIBRARY_OPENAL32")
def MENGINE_APP_LIBRARY_SDL = Utils.existAppLibrary(project, "MENGINE_APP_LIBRARY_SDL")

def MENGINE_APP_PLUGIN_SPLASHSCREEN = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_SPLASHSCREEN")
def MENGINE_APP_PLUGIN_LOCAL_NOTIFICATIONS = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_LOCAL_NOTIFICATIONS")
def MENGINE_APP_PLUGIN_ADVERTISING = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_ADVERTISING")
def MENGINE_APP_PLUGIN_GDPR = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_GDPR")
def MENGINE_APP_PLUGIN_GOOGLE_SERVICE = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_GOOGLE_SERVICE")
def MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL")
def MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING")
def MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS")
def MENGINE_APP_PLUGIN_FIREBASE = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE")
def MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS")
def MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE = Utils.getBooleanProperty(project, "MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE", false)
def MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS")
def MENGINE_APP_PLUGIN_FIREBASE_MESSAGING = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_MESSAGING")
def MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG")
def MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING")
def MENGINE_APP_PLUGIN_APPMETRICA = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPMETRICA")
def MENGINE_APP_PLUGIN_APPSFLYER = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPSFLYER")
def MENGINE_APP_PLUGIN_DEVTODEV = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_DEVTODEV")
def MENGINE_APP_PLUGIN_FACEBOOK = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FACEBOOK")
def MENGINE_APP_PLUGIN_SENTRY = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_SENTRY")
def MENGINE_APP_PLUGIN_MAR = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_MAR")
def MENGINE_APP_PLUGIN_AMAZON = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_AMAZON")
def MENGINE_APP_PLUGIN_APPLOVIN = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN")
def MENGINE_APP_PLUGIN_ADJUST = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_ADJUST")
def MENGINE_APP_PLUGIN_HELPSHIFT = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_HELPSHIFT")
def MENGINE_APP_PLUGIN_ONESIGNAL = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_ONESIGNAL")
def MENGINE_APP_PLUGIN_LEANPLUM = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_LEANPLUM")
def MENGINE_APP_PLUGIN_DATADOG = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_DATADOG")
def MENGINE_APP_PLUGIN_DEVDEBUGGER = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_DEVDEBUGGER")

Utils.logString("ANDROID_APP_DELIVERY_PACKAGES", ANDROID_APP_DELIVERY_PACKAGES)
Utils.logAvailable("ANDROID_APP_MANIFEST_CI", ANDROID_APP_MANIFEST_CI)
Utils.logAvailable("ANDROID_APP_BUILD_PUBLISH", ANDROID_APP_BUILD_PUBLISH)
Utils.logAvailable("ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE", ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE)
Utils.logAvailable("ANDROID_APP_SPLIT_ENABLE", ANDROID_APP_SPLIT_ENABLE)
Utils.logAvailable("ANDROID_APP_BUNDLE_ENABLE", ANDROID_APP_BUNDLE_ENABLE)
Utils.logInteger("ANDROID_APP_BUILD_NUMBER", ANDROID_APP_BUILD_NUMBER)

if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == true) {
    apply plugin: 'com.google.gms.google-services'
}

if (MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS == true) {
    apply plugin: 'com.google.firebase.crashlytics'
}

if (MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING == true) {
    apply plugin: 'com.google.firebase.firebase-perf'
}

if (MENGINE_APP_PLUGIN_APPLOVIN == true && project.hasProperty("MENGINE_APP_PLUGIN_APPLOVIN_AD_REVIEW_KEY") == true) {
    println "MENGINE_APP_PLUGIN_APPLOVIN_AD_REVIEW_KEY: $MENGINE_APP_PLUGIN_APPLOVIN_AD_REVIEW_KEY"

    apply plugin: 'applovin-quality-service'

    applovin {
        apiKey MENGINE_APP_PLUGIN_APPLOVIN_AD_REVIEW_KEY
    }
}

android {
    ext.plugins = []

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {
            //Empty
        } else if (taskGraph.hasTask(assembleRelease)) {
            //Empty
        } else if (taskGraph.hasTask(bundleDebug)) {
            if (ANDROID_APP_SPLIT_ENABLE == true) {
                throw new GradleException("unsupport bundle with enable ANDROID_APP_SPLIT_ENABLE");
            }

            if (ANDROID_APP_BUNDLE_ENABLE == false) {
                throw new GradleException("unsupport bundle without enable ANDROID_APP_BUNDLE_ENABLE");
            }
        } else if (taskGraph.hasTask(bundleRelease)) {
            if (ANDROID_APP_SPLIT_ENABLE == true) {
                throw new GradleException("unsupport bundle with enable ANDROID_APP_SPLIT_ENABLE");
            }

            if (ANDROID_APP_BUNDLE_ENABLE == false) {
                throw new GradleException("unsupport bundle without enable ANDROID_APP_BUNDLE_ENABLE");
            }
        }
    }

    signingConfigs {
        if (project.hasProperty("ANDROID_APP_DEBUG_STORE_FILE") == true) {
            debug {
                println "ANDROID_APP_DEBUG_STORE_FILE: $ANDROID_APP_DEBUG_STORE_FILE"
                println "ANDROID_APP_DEBUG_KEY_ALIAS: $ANDROID_APP_DEBUG_KEY_ALIAS"

                def f = new File(ANDROID_APP_DEBUG_STORE_FILE)

                if (f.exists() == false) {
                    throw new GradleException("ANDROID_APP_DEBUG_STORE_FILE not found folder: $ANDROID_APP_DEBUG_STORE_FILE");
                }

                storeFile file(ANDROID_APP_DEBUG_STORE_FILE)
                storePassword ANDROID_APP_DEBUG_STORE_PASSWORD
                keyAlias ANDROID_APP_DEBUG_KEY_ALIAS
                keyPassword ANDROID_APP_DEBUG_KEY_PASSWORD
            }
        }

        if (project.hasProperty("ANDROID_APP_RELEASE_STORE_FILE") == true) {
            release {
                println "ANDROID_APP_RELEASE_STORE_FILE: $ANDROID_APP_RELEASE_STORE_FILE"
                println "ANDROID_APP_RELEASE_KEY_ALIAS: $ANDROID_APP_RELEASE_KEY_ALIAS"

                def f = new File(ANDROID_APP_RELEASE_STORE_FILE)

                if (f.exists() == false) {
                    throw new GradleException("ANDROID_APP_DEBUG_STORE_FILE not found folder: $ANDROID_APP_RELEASE_STORE_FILE");
                }

                storeFile file(ANDROID_APP_RELEASE_STORE_FILE)
                storePassword ANDROID_APP_RELEASE_STORE_PASSWORD
                keyAlias ANDROID_APP_RELEASE_KEY_ALIAS
                keyPassword ANDROID_APP_RELEASE_KEY_PASSWORD
            }
        }
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = false
        }
    }

    defaultConfig {
        if (project.hasProperty("ANDROID_APP_ID")) {
            applicationId ANDROID_APP_ID

            println "ANDROID_APP_ID: $applicationId"
        } else if (System.getenv('ANDROID_APP_ID')) {
            applicationId System.getenv('ANDROID_APP_ID')

            println "ANDROID_APP_ID: $applicationId"
        } else {
            applicationId "org.Mengine.ANDROID_APP_ID"

            println "HARDCODE ANDROID_APP_ID: $applicationId"
        }

        def buildNumber = ANDROID_APP_BUILD_NUMBER

        if (ANDROID_APP_BUILD_PUBLISH == true) {
            if (ANDROID_APP_BUNDLE_ENABLE == true) {
                versionCode = 410000000 + buildNumber
            } else {
                versionCode = 400000000 + buildNumber
            }
        } else {
            if (ANDROID_APP_BUNDLE_ENABLE == true) {
                versionCode = 310000000 + buildNumber
            } else {
                versionCode = 300000000 + buildNumber
            }
        }

        if (project.hasProperty("ANDROID_APP_BUILD_VERSION")) {
            versionName ANDROID_APP_BUILD_VERSION
        } else {
            versionName "0.0.0"
        }

        println "versionCode: $versionCode"
        println "versionName: $versionName"
    }

    buildTypes {
        debug {
            if (project.hasProperty("ANDROID_APP_DEBUG_STORE_FILE") == true) {
                signingConfig signingConfigs.debug
            }
        }

        release {
            if (project.hasProperty("ANDROID_APP_RELEASE_STORE_FILE") == true) {
                signingConfig signingConfigs.release
            }
        }
    }

    if (MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS == true && MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE == true) {
        buildTypes {
            debug {
                firebaseCrashlytics {
                    if (ANDROID_APP_DEBUG_MINIFY_ENABLE == true) {
                        mappingFileUploadEnabled true
                    }

                    nativeSymbolUploadEnabled true

                    unstrippedNativeLibsDir "$buildDir/intermediates/merged_native_libs/debug/out/lib/"
                }
            }

            release {
                firebaseCrashlytics {
                    if (ANDROID_APP_RELEASE_MINIFY_ENABLE == true) {
                        mappingFileUploadEnabled true
                    }

                    nativeSymbolUploadEnabled true

                    unstrippedNativeLibsDir "$buildDir/intermediates/merged_native_libs/release/out/lib/"
                }
            }
        }
    }

    if (ANDROID_APP_DELIVERY_PACKAGES != null) {
        def PACKAGES = ANDROID_APP_DELIVERY_PACKAGES.split(",")

        for(PACKAGE_DESC in PACKAGES) {
            var PACKAGE_NAME = PACKAGE_DESC.split(";")[0]

            println "add delivery asset pack: $PACKAGE_NAME"

            assetPacks += [PACKAGE_NAME]
        }
    }

    sourceSets {
        main {
            if (ANDROID_APP_MANIFEST_CI == true) {
                println 'ANDROID_APP_MANIFEST_SRC_FILE: AndroidManifestCI.xml [CI]'

                manifest.srcFile 'AndroidManifestCI.xml'
            } else if (project.hasProperty("ANDROID_APP_MANIFEST_SRC_FILE")) {
                println "ANDROID_APP_MANIFEST_SRC_FILE: $ANDROID_APP_MANIFEST_SRC_FILE"

                def f = new File(ANDROID_APP_MANIFEST_SRC_FILE)

                if (f.exists() == false) {
                    throw new GradleException("ANDROID_APP_MANIFEST_SRC_FILE not found folder: $ANDROID_APP_MANIFEST_SRC_FILE");
                }

                manifest.srcFile ANDROID_APP_MANIFEST_SRC_FILE
            } else {
                println 'ANDROID_APP_MANIFEST_SRC_FILE: AndroidManifest.xml [Default]'

                manifest.srcFile 'AndroidManifest.xml'
            }

            java.srcDirs += ['src']

            if (ANDROID_APP_ASSETS_RES_CI == true) {
                println 'ANDROID_APP_ASSETS_RES_DIR: res_ci [CI]'

                res.srcDirs += ['res_ci']
            } else if (project.hasProperty("ANDROID_APP_ASSETS_RES_DIR")) {
                println "ANDROID_APP_ASSETS_RES_DIR: $ANDROID_APP_ASSETS_RES_DIR"

                def f = new File(ANDROID_APP_ASSETS_RES_DIR)

                if (f.exists() == false) {
                    throw new GradleException("ANDROID_APP_ASSETS_RES_DIR not found folder: $ANDROID_APP_ASSETS_RES_DIR");
                }

                res.srcDirs += [ANDROID_APP_ASSETS_RES_DIR]
            } else {
                println 'ANDROID_APP_ASSETS_RES_DIR: res [Default]'

                res.srcDirs += ['res']
            }

            if (project.hasProperty("ANDROID_APP_ASSETS_SRC_DIR")) {
                println "ANDROID_APP_ASSETS_SRC_DIR: $ANDROID_APP_ASSETS_SRC_DIR"

                def f = new File(ANDROID_APP_ASSETS_SRC_DIR)

                if (f.exists() == false) {
                    throw new GradleException("ANDROID_APP_ASSETS_SRC_DIR not found folder: $ANDROID_APP_ASSETS_SRC_DIR");
                }

                assets.srcDirs += [ANDROID_APP_ASSETS_SRC_DIR]
            } else {
                println 'ANDROID_APP_ASSETS_SRC_DIR: data [Default]'

                assets.srcDirs += ['data']
            }
        }
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['lib/**/libc++_shared.so', 'lib/**/libopenal.so', 'lib/**/libSDL2.so']
        }
    }

    androidResources {
        noCompress += ['pak', 'bin']
    }

    namespace "org.Mengine.Project"
}

if (ANDROID_APP_SPLIT_ENABLE == true) {
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abiCodes = ["arm64-v8a": 1, "armeabi-v7a": 2, "x86": 3, "x86_64": 4]

            def filter = output.getFilter("ABI")
            def baseAbiVersionCode = abiCodes.get(filter)

            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode * 1000000 + variant.versionCode
            }
        }
    }
}

Utils.logAvailable("MENGINE_APP_LIBRARY_SDL", MENGINE_APP_LIBRARY_SDL)

if (MENGINE_APP_LIBRARY_SDL == true) {
    dependencies {
        implementation project(':libraries:SDL')
    }
}

Utils.logAvailable("MENGINE_APP_LIBRARY_OPENAL32", MENGINE_APP_LIBRARY_OPENAL32)

if (MENGINE_APP_LIBRARY_OPENAL32 == true) {
    dependencies {
        implementation project(':libraries:OpenAL32')
    }
}

Utils.logAvailable("MENGINE_APP_LIBRARY_MENGINE", MENGINE_APP_LIBRARY_MENGINE)

if (MENGINE_APP_LIBRARY_MENGINE == true) {
    dependencies {
        implementation project(':libraries:Mengine')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_GOOGLE_SERVICE", MENGINE_APP_PLUGIN_GOOGLE_SERVICE)

if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == true) {
    dependencies {
        implementation project(':plugins:GoogleService')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_ADVERTISING", MENGINE_APP_PLUGIN_ADVERTISING)

if (MENGINE_APP_PLUGIN_ADVERTISING == true) {
    if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_ADVERTISING required enable MENGINE_APP_PLUGIN_GOOGLE_SERVICE");
    }

    android.ext.plugins += 'org.Mengine.Plugin.Advertising.MengineAdvertisingPlugin'

    dependencies {
        implementation project(':plugins:Advertising')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_GDPR", MENGINE_APP_PLUGIN_GDPR)

if (MENGINE_APP_PLUGIN_GDPR == true) {
    android.ext.plugins += 'org.Mengine.Plugin.GeneralDataProtectionRegulation.MengineGeneralDataProtectionRegulationPlugin'

    dependencies {
        implementation project(':plugins:GeneralDataProtectionRegulation')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE", MENGINE_APP_PLUGIN_FIREBASE)

if (MENGINE_APP_PLUGIN_FIREBASE == true) {
    if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE required enable MENGINE_APP_PLUGIN_GOOGLE_SERVICE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.Firebase.MengineFirebasePlugin'

    dependencies {
        implementation project(':plugins:Firebase')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS", MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS)
Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE", MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE)

if (MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS == true) {
    if (MENGINE_APP_PLUGIN_FIREBASE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS required enable MENGINE_APP_PLUGIN_FIREBASE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.FirebaseCrashlytics.MengineFirebaseCrashlyticsPlugin'

    dependencies {
        implementation project(':plugins:FirebaseCrashlytics')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS", MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS)

if (MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS == true) {
    if (MENGINE_APP_PLUGIN_FIREBASE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS required enable MENGINE_APP_PLUGIN_FIREBASE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.FirebaseAnalytics.MengineFirebaseAnalyticsPlugin'

    dependencies {
        implementation project(':plugins:FirebaseAnalytics')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_MESSAGING", MENGINE_APP_PLUGIN_FIREBASE_MESSAGING)

if (MENGINE_APP_PLUGIN_FIREBASE_MESSAGING == true) {
    if (MENGINE_APP_PLUGIN_FIREBASE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE_MESSAGING required enable MENGINE_APP_PLUGIN_FIREBASE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.FirebaseMessaging.MengineFirebaseMessagingPlugin'

    dependencies {
        implementation project(':plugins:FirebaseMessaging')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG", MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG)

if (MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG == true) {
    if (MENGINE_APP_PLUGIN_FIREBASE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE_REMOTECONFIG required enable MENGINE_APP_PLUGIN_FIREBASE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.FirebaseRemoteConfig.MengineFirebaseRemoteConfigPlugin'

    dependencies {
        implementation project(':plugins:FirebaseRemoteConfig')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING", MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING)

if (MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING == true) {
    if (MENGINE_APP_PLUGIN_FIREBASE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_FIREBASE_PERFORMANCEMONITORING required enable MENGINE_APP_PLUGIN_FIREBASE");
    }
    
    android.ext.plugins += 'org.Mengine.Plugin.FirebasePerformanceMonitoring.MengineFirebasePerformanceMonitoringPlugin'

    dependencies {
        implementation project(':plugins:FirebasePerformanceMonitoring')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_APPMETRICA", MENGINE_APP_PLUGIN_APPMETRICA)

if (MENGINE_APP_PLUGIN_APPMETRICA == true) {
    android.ext.plugins += 'org.Mengine.Plugin.AppMetrica.MengineAppMetricaPlugin'

    dependencies {
        implementation project(':plugins:AppMetrica')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_APPSFLYER", MENGINE_APP_PLUGIN_APPSFLYER)

if (MENGINE_APP_PLUGIN_APPSFLYER == true) {
    android.ext.plugins += 'org.Mengine.Plugin.AppsFlyer.MengineAppsFlyerPlugin'

    dependencies {
        implementation project(':plugins:AppsFlyer')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_DEVTODEV", MENGINE_APP_PLUGIN_DEVTODEV)

if (MENGINE_APP_PLUGIN_DEVTODEV == true) {
    android.ext.plugins += 'org.Mengine.Plugin.DevToDev.MengineDevToDevPlugin'

    dependencies {
        implementation project(':plugins:DevToDev')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_DATADOG", MENGINE_APP_PLUGIN_DATADOG)

if (MENGINE_APP_PLUGIN_DATADOG == true) {
    android.ext.plugins += 'org.Mengine.Plugin.DataDog.MengineDataDogPlugin'

    dependencies {
        implementation project(':plugins:DataDog')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_SENTRY", MENGINE_APP_PLUGIN_SENTRY)

if (MENGINE_APP_PLUGIN_SENTRY == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Sentry.MengineSentryPlugin'

    dependencies {
        implementation project(':plugins:Sentry')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_ADJUST", MENGINE_APP_PLUGIN_ADJUST)

if (MENGINE_APP_PLUGIN_ADJUST == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Adjust.MengineAdjustPlugin'

    dependencies {
        implementation project(':plugins:Adjust')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_SPLASHSCREEN", MENGINE_APP_PLUGIN_SPLASHSCREEN)

if (MENGINE_APP_PLUGIN_SPLASHSCREEN == true) {
    android.ext.plugins += 'org.Mengine.Plugin.SplashScreen.MengineSplashScreenPlugin'

    dependencies {
        implementation project(':plugins:SplashScreen')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL", MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL)

if (MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL == true) {
    if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_GOOGLE_GAME_SOCIAL required enable MENGINE_APP_PLUGIN_GOOGLE_SERVICE");
    }

    android.ext.plugins += 'org.Mengine.Plugin.GoogleGameSocial.MengineGoogleGameSocialPlugin'

    dependencies {
        implementation project(':plugins:GoogleGameSocial')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING", MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING)

if (MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING == true) {
    if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_GOOGLE_PLAY_BILLING required enable MENGINE_APP_PLUGIN_GOOGLE_SERVICE");
    }

    android.ext.plugins += 'org.Mengine.Plugin.GooglePlayBilling.MengineGooglePlayBillingPlugin'

    dependencies {
        implementation project(':plugins:GooglePlayBilling')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS", MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS)

if (MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS == true) {
    if (MENGINE_APP_PLUGIN_GOOGLE_SERVICE == false) {
        throw new GradleException("MENGINE_APP_PLUGIN_GOOGLE_INAPP_REVIEWS required enable MENGINE_APP_PLUGIN_GOOGLE_SERVICE");
    }

    android.ext.plugins += 'org.Mengine.Plugin.GoogleInAppReviews.MengineGoogleInAppReviewsPlugin'

    dependencies {
        implementation project(':plugins:GoogleInAppReviews')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_FACEBOOK", MENGINE_APP_PLUGIN_FACEBOOK)

if (MENGINE_APP_PLUGIN_FACEBOOK == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Facebook.MengineFacebookPlugin'

    dependencies {
        implementation project(':plugins:Facebook')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_LOCAL_NOTIFICATIONS", MENGINE_APP_PLUGIN_LOCAL_NOTIFICATIONS)

if (MENGINE_APP_PLUGIN_LOCAL_NOTIFICATIONS == true) {
    android.ext.plugins += 'org.Mengine.Plugin.LocalNotifications.MengineLocalNotificationsPlugin'

    dependencies {
        implementation project(':plugins:LocalNotifications')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_MAR", MENGINE_APP_PLUGIN_MAR)

if (MENGINE_APP_PLUGIN_MAR == true) {
    android.ext.plugins += 'org.Mengine.Plugin.MAR.MengineMARPlugin'

    dependencies {
        implementation project(':plugins:MAR')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_AMAZON", MENGINE_APP_PLUGIN_AMAZON)

if (MENGINE_APP_PLUGIN_AMAZON == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Amazon.MengineAmazonPlugin'

    dependencies {
        implementation project(':plugins:Amazon')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_APPLOVIN", MENGINE_APP_PLUGIN_APPLOVIN)

if (MENGINE_APP_PLUGIN_APPLOVIN == true) {
    android.ext.plugins += 'org.Mengine.Plugin.AppLovin.MengineAppLovinPlugin'

    dependencies {
        implementation project(':plugins:AppLovin')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_HELPSHIFT", MENGINE_APP_PLUGIN_HELPSHIFT)

if (MENGINE_APP_PLUGIN_HELPSHIFT == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Helpshift.MengineHelpshiftPlugin'

    dependencies {
        implementation project(':plugins:Helpshift')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_ONESIGNAL", MENGINE_APP_PLUGIN_ONESIGNAL)

if (MENGINE_APP_PLUGIN_ONESIGNAL == true) {
    android.ext.plugins += 'org.Mengine.Plugin.OneSignal.MengineOneSignalPlugin'

    dependencies {
        implementation project(':plugins:OneSignal')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_LEANPLUM", MENGINE_APP_PLUGIN_LEANPLUM)

if (MENGINE_APP_PLUGIN_LEANPLUM == true) {
    android.ext.plugins += 'org.Mengine.Plugin.Leanplum.MengineLeanplumPlugin'

    dependencies {
        implementation project(':plugins:Leanplum')
    }
}

Utils.logAvailable("MENGINE_APP_PLUGIN_DEVDEBUGGER", MENGINE_APP_PLUGIN_DEVDEBUGGER)

if (MENGINE_APP_PLUGIN_DEVDEBUGGER == true) {
    android.ext.plugins += 'org.Mengine.Plugin.DevDebugger.MengineDevDebuggerPlugin'

    dependencies {
        implementation project(':plugins:DevDebugger')
    }
}

if (project.hasProperty("MENGINE_APP_OPTIONS")) {
    println "MENGINE_APP_OPTIONS: $MENGINE_APP_OPTIONS"

    android {
        defaultConfig {
            buildConfigField "String", "MENGINE_APP_OPTIONS", "\"${MENGINE_APP_OPTIONS}\""
        }
    }
}

android {
    defaultConfig {
        buildConfigField "boolean", "ANDROID_APP_BUILD_PUBLISH", "${ANDROID_APP_BUILD_PUBLISH}"
    }
}

android {
    defaultConfig {
        buildConfigField "String[]", "MENGINE_GRADLE_ANDROID_PLUGINS", '{' + android.ext.plugins.collect {"\"${it}\""}.join(",") + '}'
    }
}

if (MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS == true && MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS_NATIVE_SYMBOL_UPLOAD_ENABLE == true) {
    tasks.all { task ->
        if (task.name.startsWith('generateCrashlyticsSymbolFile')) {
            task.dependsOn 'merge' + (task.name =~ /^generateCrashlyticsSymbolFile(.+)$/)[0][1] + 'NativeLibs'
        }
    }

    tasks.all { task ->
        if (task.name.startsWith('injectCrashlyticsBuildIds')) {
            task.dependsOn 'merge' + (task.name =~ /^injectCrashlyticsBuildIds(.+)$/)[0][1] + 'NativeLibs'
        }
    }
}