apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

android {
    signingConfigs {
        config {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                print 'RELEASE_STORE_FILE = '
                println RELEASE_STORE_FILE

                print 'RELEASE_KEY_ALIAS = '
                println RELEASE_KEY_ALIAS

                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildToolsVersion '28.0.3'
    compileSdkVersion 28
    defaultConfig {
        if (findProperty("ANDROID_APP_ID")) {
            applicationId ANDROID_APP_ID
            print 'ANDROID_APP_ID = '
        } else if (System.getenv('MENGINE_ANDROID_APP_ID')) {
            applicationId System.getenv('MENGINE_ANDROID_APP_ID')
            print 'MENGINE_ANDROID_APP_ID = '
        } else {
            applicationId "org.Mengine.EvilMonkey"
            print 'HARDCODE ANDROID_APP_ID = '
        }
        
        println applicationId

        minSdkVersion 18

        if (findProperty("ANDROID_APP_CODE_VERSION")) {
            versionCode ANDROID_APP_CODE_VERSION as Integer
            versionName ANDROID_APP_CODE_VERSION
        } else {
            versionCode 4
            versionName "4"
        }
        if (findProperty("ANDROID_APP_NAME")) {
            manifestPlaceholders = [app_name_gradle:ANDROID_APP_NAME]
        } else {
            manifestPlaceholders = [app_name_gradle:"org.Mengine.EvilMonkey"]
        }

        print 'manifestPlaceholders = '
        println manifestPlaceholders
        
        print 'ANDROID_APP_CODE_VERSION = '
        println versionCode
        
        print 'ANDROID_APP_NAME_VERSION = '
        println versionName
        
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
        signingConfig signingConfigs.config
        targetSdkVersion 28

        multiDexEnabled true
    }
    
    externalNativeBuild {
        cmake {
            version "3.10.2"
        }
    }
    
    buildTypes {
        debug {
            debuggable true
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-18",
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=TRUE",
                            "-DCMAKE_BUILD_TYPE:STRING=Debug",
                            "-DCMAKE_CONFIGURATION_TYPES:STRING=Debug"
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-18",
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=TRUE",
                            "-DCMAKE_BUILD_TYPE:STRING=Release",
                            "-DCMAKE_CONFIGURATION_TYPES:STRING=Release"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "../../CMake/Android_SDL/CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', '../../dependencies/SDL2/android-project/app/src/main/java/']
            
            if (findProperty("ASSETS_RES_DIR")) {
                print 'ASSETS_RES_DIR = '
                println ASSETS_RES_DIR

                res.srcDirs = [ASSETS_RES_DIR]
            } else {
                res.srcDirs = ['res']
            }

			if (findProperty("ASSETS_SRC_DIR")) {
                print 'ASSETS_SRC_DIR = '
                println ASSETS_SRC_DIR

                assets.srcDirs = [ASSETS_SRC_DIR]
            } else {
                assets.srcDirs = ['data']
            }
        }
    }
    aaptOptions {
        noCompress 'pak', 'bin'
    }
    productFlavors {
    }    
    packagingOptions {
       pickFirst 'lib/arm64-v8a/libjsc.so'
       pickFirst 'lib/arm64-v8a/libc++_shared.so'
       pickFirst 'lib/armeabi-v7a/libc++_shared.so'
    }
}

task openal_copy_bsinc_inc(type: Copy) {
    from "../../cmake/Dependencies/openal-soft/bsinc_inc.h"
    into "../../dependencies/openal-soft"
}

task openal_CMakeLists(type: Copy) {
    from "../../cmake/Dependencies/openal-soft/CMakeLists.txt"
    into "../../dependencies/openal-soft"
}

preBuild.dependsOn openal_copy_bsinc_inc
preBuild.dependsOn openal_CMakeLists

crashlytics {
    enableNdk true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation "androidx.core:core:1.0.0"
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.devtodev:android:1.13'
    implementation 'com.google.android.gms:play-services-base:17.1.0'
    implementation 'com.google.android.gms:play-services-ads:18.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.facebook.android:facebook-android-sdk:5.15.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation project(':libraries:SDL2')
    implementation project(':libraries:OpenAL32')
    implementation project(':unity-ads')
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    implementation 'com.google.firebase:firebase-firestore:21.4.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.1'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'androidx.multidex:multidex:2.0.0'
}
apply plugin: 'com.google.gms.google-services'

