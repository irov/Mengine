apply plugin: 'com.android.application'

android {
    ndkVersion '22.0.7026061'
    buildToolsVersion '30.0.3'
    compileSdkVersion 30

    ext.getBooleanProperty = { name, d ->
        def p = project.getProperties().get(name, null)

        if( p == null )
        {
            return d
        }

        if( p as Integer == 0 )
        {
            return false
        }

        return true
    }

    signingConfigs {
        config {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                print 'RELEASE_STORE_FILE = '
                println RELEASE_STORE_FILE

                print 'RELEASE_KEY_ALIAS = '
                println RELEASE_KEY_ALIAS

                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }   
    
    defaultConfig {
        if (project.hasProperty("ANDROID_APP_ID")) {
            applicationId ANDROID_APP_ID
            print 'ANDROID_APP_ID = '
        } else if (System.getenv('ANDROID_APP_ID')) {
            applicationId System.getenv('ANDROID_APP_ID')
            print 'ANDROID_APP_ID = '
        } else {
            applicationId "org.Mengine.ANDROID_APP_ID"
            print 'HARDCODE ANDROID_APP_ID = '
        }
        
        println applicationId

        minSdkVersion 19
        targetSdkVersion 30
        
        print 'ANDROID_APP_BUILD_PUBLISH = '
        println getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false)
        
        print 'ANDROID_APP_BUILD_NUMBER = '
        println project.getProperties().get("ANDROID_APP_BUILD_NUMBER", "0")
        
        def buildNumber = 0
        if (project.hasProperty("ANDROID_APP_BUILD_NUMBER")) {
            buildNumber = ANDROID_APP_BUILD_NUMBER as Integer
        }

        if (getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false) == true) {
            versionCode = 400000000 + buildNumber
        } else {
            versionCode = 300000000 + buildNumber
        }
        
        if (project.hasProperty("ANDROID_APP_VERSION_NAME")) {
            versionName ANDROID_APP_VERSION_NAME
        } else {
            versionName versionCode as String
        }
        
        def app_name_gradle = "org.Wonderland.EvilMonkey"
        
        if (project.hasProperty("ANDROID_APP_NAME")) {
            app_name_gradle = ANDROID_APP_NAME
        }
        
        print 'app_name_gradle = '
        println app_name_gradle
        
        def app_screen_orientation = "fullSensor"
        
        if (project.hasProperty("ANDROID_APP_SCREEN_ORIENTATION")) {
            app_screen_orientation = ANDROID_APP_SCREEN_ORIENTATION
        }
        
        print 'app_screen_orientation = '
        println app_screen_orientation
        
        manifestPlaceholders = [app_name_gradle:app_name_gradle, app_screen_orientation:app_screen_orientation]
        
        print 'manifestPlaceholders = '
        println manifestPlaceholders
        
        print 'versionCode = '
        println versionCode
        
        print 'versionName = '
        println versionName
        
        signingConfig signingConfigs.config

        multiDexEnabled true
        
        print 'ANDROID_APP_SPLIT_ENABLE = '
        println getBooleanProperty("ANDROID_APP_SPLIT_ENABLE", false)
        
        print 'ANDROID_APP_EMULATOR_ENABLE = '
        println getBooleanProperty("ANDROID_APP_EMULATOR_ENABLE", false)
    }

    splits {
        abi {
            if (getBooleanProperty("ANDROID_APP_SPLIT_ENABLE", false) == true) {
                enable true
            } else {
                enable false
            }
            
            reset()

            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"

            universalApk false
        }
    }
    
    buildTypes {
        debug {
            debuggable true
            
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-19",
                        "-DANDROID_ARM_NEON=TRUE",
                        "-DANDROID_STL=c++_shared", 
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_ALLOW_UNDEFINED_SYMBOLS:BOOLEAN=TRUE",
                        "-DMENGINE_BUILD_MENGINE_BUILD_PUBLISH=" + (getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false) ? "ON" : "OFF"),
                        "-DMENGINE_BUILD_MENGINE_DEVELOPMENT=ON",
                        "-DCMAKE_BUILD_TYPE:STRING=Debug"
                        
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-19",
                        "-DANDROID_ARM_NEON=TRUE",
                        "-DANDROID_STL=c++_shared",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_ALLOW_UNDEFINED_SYMBOLS:BOOLEAN=TRUE",
                        "-DMENGINE_BUILD_MENGINE_BUILD_PUBLISH=" + (getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false) ? "ON" : "OFF"),
                        "-DMENGINE_BUILD_MENGINE_DEVELOPMENT=OFF",
                        "-DCMAKE_BUILD_TYPE:STRING=Release"
                }
            }
        }
    }
    
    externalNativeBuild {
        cmake {
            path "../../cmake/Android_SDL/CMakeLists.txt"
            version "3.10.2"
        }
    }
    
    sourceSets {
        main {
            if (project.hasProperty("MANIFEST_SRC_FILE")) {
                print 'MANIFEST_SRC_FILE = '
                println MANIFEST_SRC_FILE
            
                manifest.srcFile MANIFEST_SRC_FILE
            } else if (getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false) == true) {
                println 'MANIFEST_SRC_FILE = AndroidManifestPublish.xml'
            
                manifest.srcFile 'AndroidManifestPublish.xml'
            } else {
                println 'MANIFEST_SRC_FILE = AndroidManifest.xml'
                
                manifest.srcFile 'AndroidManifest.xml'
            }

            if (project.hasProperty("ASSETS_JAVA_NO_SRC_DIR")) {
                println 'ASSETS_JAVA_SRC_DIR = [No]'

                java.srcDirs = ['../../dependencies/SDL2/android-project/app/src/main/java/']
            } else if (project.hasProperty("ASSETS_JAVA_SRC_DIR")) {
                print 'ASSETS_JAVA_SRC_DIR = '
                println ASSETS_JAVA_SRC_DIR

                java.srcDirs = [ASSETS_JAVA_SRC_DIR, '../../dependencies/SDL2/android-project/app/src/main/java/']
            }
            else {
                java.srcDirs = ['src', '../../dependencies/SDL2/android-project/app/src/main/java/']
            }
            
            if (project.hasProperty("ASSETS_RES_DIR")) {
                print 'ASSETS_RES_DIR = '
                println ASSETS_RES_DIR

                res.srcDirs = [ASSETS_RES_DIR]
            } else {
                res.srcDirs = ['res']
            }

			if (project.hasProperty("ASSETS_SRC_DIR")) {
                print 'ASSETS_SRC_DIR = '
                println ASSETS_SRC_DIR

                assets.srcDirs = [ASSETS_SRC_DIR]
            } else {
                print 'ASSETS_SRC_DIR = '
                println 'data'
                
                assets.srcDirs = ['data']
            }
        }
    }
    
    aaptOptions {
        noCompress 'pak', 'bin'
    }
    
    packagingOptions {
        pickFirst 'lib/**/libc++_shared.so'
        pickFirst 'lib/**/libopenal.so'
        pickFirst 'lib/**/libSDL2.so'
        pickFirst 'lib/**/libhidapi.so'
    }
    
    afterEvaluate {
        if (getBooleanProperty("ANDROID_APP_UPLOAD_CRASHLYTICS", false) == true) {
            build.finalizedBy(uploadCrashlyticsSymbolFileRelease)
        }
    }
}

ext.abiCodes = ["arm64-v8a": 1, "armeabi-v7a": 2, "x86": 3, "x86_64": 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->    
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        if (baseAbiVersionCode != null) {
            output.versionCodeOverride =
                    baseAbiVersionCode * 1000000 + variant.versionCode
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.core:core:1.5.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.annotation:annotation:1.2.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation project(':libraries:SDL2')
    implementation project(':libraries:OpenAL32')
}


if( android.getBooleanProperty("ANDROID_APP_ENABLE_UNITYADS", true) == true ) {
    println 'ANDROID_APP_ENABLE_UNITYADS: TRUE'

    dependencies {
        implementation project(':libraries:unity-ads')
    }
}
else
{
    println 'ANDROID_APP_ENABLE_UNITYADS: FALSE'
}

if( android.getBooleanProperty("ANDROID_APP_ENABLE_DEVTODEV", true) == true ) {
    println 'ANDROID_APP_ENABLE_DEVTODEV: TRUE'

    dependencies {
        implementation project(':libraries:devtodev')
    }
}
else
{
    println 'ANDROID_APP_ENABLE_DEVTODEV: FALSE'
}

if( android.getBooleanProperty("ANDROID_APP_ENABLE_FACEBOOK", true) == true ) {
    println 'ANDROID_APP_ENABLE_FACEBOOK: TRUE'

    dependencies {
        implementation 'com.facebook.android:facebook-android-sdk:9.0.0'
    }
}
else
{
    println 'ANDROID_APP_ENABLE_FACEBOOK: FALSE'
}

if( android.getBooleanProperty("ANDROID_APP_ENABLE_GOOGLE_SERVICE", true) == true ) {
    println 'ANDROID_APP_ENABLE_GOOGLE_SERVICE: TRUE'

    apply plugin: 'com.google.gms.google-services'

    dependencies {
        implementation 'com.google.android.gms:play-services-gcm:17.0.0'
        implementation 'com.google.android.gms:play-services-ads:19.7.0' // issue https://github.com/irov/Mengine/issues/89
        implementation 'com.google.android.gms:play-services-basement:17.6.0'
    }
}
else
{
    println 'ANDROID_APP_ENABLE_GOOGLE_SERVICE: FALSE'
}

if( android.getBooleanProperty("ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS", true) == true ) {
    println 'ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS: TRUE'
    
    apply plugin: 'com.google.firebase.crashlytics'

    android {
        if (getBooleanProperty("ANDROID_APP_UPLOAD_CRASHLYTICS", false) == true) {
            buildTypes {
                debug {
                    externalNativeBuild {
                        firebaseCrashlytics {
                            mappingFileUploadEnabled true
                        }
                    }
                }

                release {
                    externalNativeBuild {
                        firebaseCrashlytics {
                            nativeSymbolUploadEnabled true
                        }
                    }
                }
            }
        }
    }
    
    dependencies {
        implementation platform('com.google.firebase:firebase-bom:26.3.0')

        implementation 'com.google.firebase:firebase-crashlytics'
        implementation 'com.google.firebase:firebase-analytics'
    }    
}
else
{
    println 'ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS: FALSE'
}

if( android.getBooleanProperty("ANDROID_APP_ENABLE_SENTRY", true) == true ) {
    println 'ANDROID_APP_ENABLE_SENTRY: TRUE'

    apply plugin: 'io.sentry.android.gradle'

    dependencies {
        implementation 'io.sentry:sentry-android:4.1.0'

    }

    sentry {
        autoProguardConfig true
        autoUpload true
        uploadNativeSymbols true
        includeNativeSources true
    }
}
else
{
    println 'ANDROID_APP_ENABLE_SENTRY: FALSE'
}