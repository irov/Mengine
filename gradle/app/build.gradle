apply plugin: 'com.android.application'

import com.Mengine.Utils

android {
    ext.plugins = []

    ndkVersion project.ndkVersion
    buildToolsVersion project.buildToolsVersion
    compileSdkVersion project.compileSdkVersion

    signingConfigs {
        config {
            if (project.hasProperty("ANDROID_APP_RELEASE_STORE_FILE")) {
                print 'ANDROID_APP_RELEASE_STORE_FILE = '
                println ANDROID_APP_RELEASE_STORE_FILE

                print 'ANDROID_APP_RELEASE_KEY_ALIAS = '
                println ANDROID_APP_RELEASE_KEY_ALIAS

                storeFile file(ANDROID_APP_RELEASE_STORE_FILE)
                storePassword ANDROID_APP_RELEASE_STORE_PASSWORD
                keyAlias ANDROID_APP_RELEASE_KEY_ALIAS
                keyPassword ANDROID_APP_RELEASE_KEY_PASSWORD
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = false
        }
    }

    defaultConfig {
        if (project.hasProperty("ANDROID_APP_ID")) {
            applicationId ANDROID_APP_ID
            print 'ANDROID_APP_ID = '
        } else if (System.getenv('ANDROID_APP_ID')) {
            applicationId System.getenv('ANDROID_APP_ID')
            print 'ANDROID_APP_ID = '
        } else {
            applicationId "org.Mengine.ANDROID_APP_ID"
            print 'HARDCODE ANDROID_APP_ID = '
        }

        println applicationId

        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        print 'ANDROID_APP_BUILD_NUMBER = '
        println project.getProperties().get("ANDROID_APP_BUILD_NUMBER", "0")

        def buildNumber = 0
        if (project.hasProperty("ANDROID_APP_BUILD_NUMBER")) {
            buildNumber = ANDROID_APP_BUILD_NUMBER as Integer
        }

        if (Utils.getBooleanProperty(project, "ANDROID_APP_BUILD_PUBLISH", false) == true) {
            versionCode = 400000000 + buildNumber
        } else {
            versionCode = 300000000 + buildNumber
        }

        if (project.hasProperty("ANDROID_APP_VERSION_NAME")) {
            versionName ANDROID_APP_VERSION_NAME
        } else {
            versionName versionCode as String
        }

        print 'versionCode = '
        println versionCode

        print 'versionName = '
        println versionName

        signingConfig signingConfigs.config

        multiDexEnabled true

        print 'ANDROID_APP_SPLIT_ENABLE = '
        println Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false)

        if (Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false) == false) {
            ndk {
                if (Utils.getBooleanProperty(project, "ANDROID_APP_ARCH_86_64_ENABLE", true) == true) {
                    abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
                } else {
                    abiFilters "arm64-v8a", "armeabi-v7a"
                }
            }
        }

        print 'ANDROID_APP_ARCH_86_64_ENABLE = '
        println Utils.getBooleanProperty(project, "ANDROID_APP_ARCH_86_64_ENABLE", true)
    }

    splits {
        abi {
            if (Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false) == true) {
                enable true
            } else {
                enable false
            }

            reset()

            if (Utils.getBooleanProperty(project,"ANDROID_APP_ARCH_86_64_ENABLE", true) == true) {
                include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            } else {
                include "arm64-v8a", "armeabi-v7a"
            }

            universalApk false
        }
    }

    if (Utils.getBooleanProperty(project, "ANDROID_APP_UPLOAD_CRASHLYTICS", false) == true) {
        buildTypes {
            debug {
                externalNativeBuild {
                    firebaseCrashlytics {
                        mappingFileUploadEnabled true
                    }
                }
            }

            release {
                externalNativeBuild {
                    firebaseCrashlytics {
                        nativeSymbolUploadEnabled true
                    }
                }
            }
        }
    }

    if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_DELIVERY_PACKAGE", false) == true) {
        print 'ANDROID_APP_DELIVERY_PACKAGE_NAME = '
        println ANDROID_APP_DELIVERY_PACKAGE_NAME
        
        assetPacks += [ANDROID_APP_DELIVERY_PACKAGE_NAME]
    }

    sourceSets {
        main {
            if (project.hasProperty("ANDROID_APP_MANIFEST_SRC_FILE")) {
                print 'ANDROID_APP_MANIFEST_SRC_FILE = '
                println ANDROID_APP_MANIFEST_SRC_FILE

                manifest.srcFile ANDROID_APP_MANIFEST_SRC_FILE
            } else {
                println 'ANDROID_APP_MANIFEST_SRC_FILE = AndroidManifest.xml [Default]'

                manifest.srcFile 'AndroidManifest.xml'
            }

            java.srcDirs += ['src']

            if (project.hasProperty("ANDROID_APP_ASSETS_RES_DIR")) {
                print 'ANDROID_APP_ASSETS_RES_DIR = '
                println ANDROID_APP_ASSETS_RES_DIR

                res.srcDirs += [ANDROID_APP_ASSETS_RES_DIR]
            } else {
                println 'ANDROID_APP_ASSETS_RES_DIR = res [Default]'

                res.srcDirs += ['res']
            }

            if (project.hasProperty("ANDROID_APP_ASSETS_SRC_DIR")) {
                print 'ANDROID_APP_ASSETS_SRC_DIR = '
                println ANDROID_APP_ASSETS_SRC_DIR

                assets.srcDirs += [ANDROID_APP_ASSETS_SRC_DIR]
            } else {
                print 'ANDROID_APP_ASSETS_SRC_DIR = '
                println 'data'

                assets.srcDirs += ['data']
            }
        }
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['lib/**/libc++_shared.so', 'lib/**/libopenal.so', 'lib/**/libSDL2.so']
        }
    }


// test c++ debug -> preBuild.dependsOn ":libraries:Mengine:build", ":libraries:SDL2:build", ":libraries:OpenAL32:build"

    afterEvaluate {
        if (Utils.getBooleanProperty(project, "ANDROID_APP_UPLOAD_CRASHLYTICS", false) == true) {
            build.finalizedBy(uploadCrashlyticsSymbolFileRelease)
        }
    }
    androidResources {
        noCompress 'pak', 'bin'
    }
}

ext.abiCodes = ["arm64-v8a": 1, "armeabi-v7a": 2, "x86": 3, "x86_64": 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        if (baseAbiVersionCode != null) {
            output.versionCodeOverride =
                    baseAbiVersionCode * 1000000 + variant.versionCode
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.8.0'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.preference:preference:1.2.0"


    implementation project(':libraries:SDL2')
    implementation project(':libraries:OpenAL32')
    implementation project(':libraries:Mengine')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_DEVTODEV", true) == true) {
    println 'ANDROID_APP_ENABLE_DEVTODEV: TRUE'

    dependencies {
        implementation project(':libraries:DevToDev')
    }
} else {
    println 'ANDROID_APP_ENABLE_DEVTODEV: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_FACEBOOK", true) == true) {
    println 'ANDROID_APP_ENABLE_FACEBOOK: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.Facebook.MengineFacebookPlugin'

    dependencies {
        implementation project(':libraries:Facebook')
    }
} else {
    println 'ANDROID_APP_ENABLE_FACEBOOK: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_GOOGLE_SERVICE", true) == true) {
    println 'ANDROID_APP_ENABLE_GOOGLE_SERVICE: TRUE'

    dependencies {
        implementation project(':libraries:GoogleService')
    }
} else {
    println 'ANDROID_APP_ENABLE_GOOGLE_SERVICE: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS", true) == true) {
    println 'ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS: TRUE'

    dependencies {
        implementation project(':libraries:FirebaseCrashlytics')
    }
} else {
    println 'ANDROID_APP_ENABLE_FIREBASE_CRASHLYTICS: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_FIREBASE_ANALYTICS", true) == true) {
    println 'ANDROID_APP_ENABLE_FIREBASE_ANALYTICS: TRUE'

    dependencies {
        implementation project(':libraries:FirebaseAnalytics')
    }
} else {
    println 'ANDROID_APP_ENABLE_FIREBASE_ANALYTICS: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_LOCAL_NOTIFICATIONS", true) == true) {
    println 'ANDROID_APP_ENABLE_LOCAL_NOTIFICATIONS: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.LocalNotifications.MengineLocalNotificationsPlugin'

    dependencies {
        implementation project(':libraries:LocalNotifications')
    }
} else {
    println 'ANDROID_APP_ENABLE_LOCAL_NOTIFICATIONS: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_SENTRY", true) == true) {
    println 'ANDROID_APP_ENABLE_SENTRY: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.Sentry.MengineSentryPlugin'

    dependencies {
        implementation project(':libraries:Sentry')
    }
} else {
    println 'ANDROID_APP_ENABLE_SENTRY: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_MAR", false) == true) {
    println 'ANDROID_APP_ENABLE_MAR: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.MAR.MengineMARPlugin'

    dependencies {
        implementation project(':libraries:MAR')
    }
} else {
    println 'ANDROID_APP_ENABLE_MAR: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_GOOGLE_BILLING", true) == true) {
    println 'ANDROID_APP_ENABLE_GOOGLE_BILLING: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.GooglePlayBilling.MengineGooglePlayBillingPlugin'

    dependencies {
        implementation project(':libraries:GoogleBilling')
    }
} else {
    println 'ANDROID_APP_ENABLE_GOOGLE_BILLING: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_APPLOVIN", true) == true) {
    println 'ANDROID_APP_ENABLE_APPLOVIN: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.Applovin.MengineApplovinPlugin'

    dependencies {
        implementation project(':libraries:Applovin')
    }
} else {
    println 'ANDROID_APP_ENABLE_APPLOVIN: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_GOOGLE_GAME_SOCIAL", true) == true) {
    println 'ANDROID_APP_ENABLE_GOOGLE_GAME_SOCIAL: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.GoogleGameSocial.MengineGoogleGameSocialPlugin'

    dependencies {
        implementation project(':libraries:GoogleGameSocial')
    }
} else {
    println 'ANDROID_APP_ENABLE_GOOGLE_GAME_SOCIAL: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_GOOGLE_INAPP_REVIEWS", true) == true) {
    println 'ANDROID_APP_ENABLE_GOOGLE_INAPP_REVIEWS: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.GoogleInAppReviews.MengineGoogleInAppReviewsPlugin'

    dependencies {
        implementation project(':libraries:GoogleInAppReviews')
    }
} else {
    println 'ANDROID_APP_ENABLE_GOOGLE_INAPP_REVIEWS: FALSE'
}

if (Utils.getBooleanProperty(project, "ANDROID_APP_ENABLE_ADJUST", false) == true) {
    println 'ANDROID_APP_ENABLE_ADJUST: TRUE'

    android.ext.plugins += 'org.Mengine.Plugin.Adjust.MengineAdjustPlugin'

    dependencies {
        implementation project(':libraries:Adjust')
    }
} else {
    println 'ANDROID_APP_ENABLE_ADJUST: FALSE'
}

if(project.hasProperty("MENGINE_APP_OPTIONS")) {
    print 'MENGINE_APP_OPTIONS: '
    println MENGINE_APP_OPTIONS

    android {
        buildTypes {
            debug {
                buildConfigField "String", "MENGINE_APP_OPTIONS", "\"${MENGINE_APP_OPTIONS}\""
            }

            release {
                buildConfigField "String", "MENGINE_APP_OPTIONS", "\"${MENGINE_APP_OPTIONS}\""
            }
        }
    }
}

android {
    buildTypes {
        debug {
            buildConfigField "String[]", "MENGINE_GRADLE_ANDROID_PLUGINS", '{' + android.ext.plugins.collect {
                "\"${it}\""
            }.join(",") + '}'
        }

        release {
            buildConfigField "String[]", "MENGINE_GRADLE_ANDROID_PLUGINS", '{' + android.ext.plugins.collect {
                "\"${it}\""
            }.join(",") + '}'
        }
    }
}