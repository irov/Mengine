import com.Mengine.Utils

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS") == true) {
            maven { url 'https://artifacts.applovin.com/android' }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.1'
        classpath 'com.google.gms:google-services:4.3.15'

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_CRASHLYTICS") == true) {
            classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_ANALYTICS") == true) {
            classpath 'com.applovin.quality:AppLovinQualityServiceGradlePlugin:4.8.3'
        }
    }
}

allprojects {
    project.ext {
        ndkVersion = '25.1.8937393'
        buildToolsVersion = '33.0.2'
        cmakeVersion = '3.22.1'
        minSdkVersion = 21
        targetSdkVersion = 33
        compileSdkVersion = 33
        javaVersion = JavaVersion.VERSION_11
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_BIDMACHINE") == true) {
            maven { url "https://artifactory.bidmachine.io/bidmachine" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_IRONSOURCE") == true) {
            maven { url "https://android-sdk.is.com" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_MAIO") == true) {
            maven { url "https://imobile-maio.github.io/maven" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_MINTEGRAL") == true) {
            maven { url "https://dl-maven-android.mintegral.com/repository/mbridge_android_sdk_oversea" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_NEND") == true) {
            maven { url "https://fan-adn.github.io/nendSDK-Android-lib/library" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_OGURY") == true) {
            maven { url "https://maven.ogury.co" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_PANGLE") == true) {
            maven { url "https://artifact.bytedance.com/repository/pangle" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_SMAATO") == true) {
            maven { url "https://s3.amazonaws.com/smaato-sdk-releases/" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_TAPJOY") == true) {
            maven { url "https://sdk.tapjoy.com" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_VERVE") == true) {
            maven { url "https://verve.jfrog.io/artifactory/verve-gradle-release" }
        }

        if (Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_APPLOVIN_MEDIATION_YAHOO") == true) {
            maven { url "https://artifactory.yahooinc.com/artifactory/maven" }
        }
    }
}