apply from: rootProject.projectDir.getAbsolutePath() + '/libraries/library.gradle'
apply from: rootProject.projectDir.getAbsolutePath() + '/gms.gradle'

final Integer ANDROID_APP_BUILD_NUMBER = Utils.getIntegerProperty("ANDROID_APP_BUILD_NUMBER", 0)
final String ANDROID_APP_BUILD_VERSION = Utils.getStringProperty("ANDROID_APP_BUILD_VERSION", "0.0.0")
final Boolean ANDROID_APP_BUILD_PUBLISH = Utils.getBooleanProperty("ANDROID_APP_BUILD_PUBLISH", false)
final String MENGINE_APP_SECURE_VALUE = Utils.getStringProperty("MENGINE_APP_SECURE_VALUE", "0123456789A")
final String ANDROID_APP_MAIN_PROJECT = Utils.getStringProperty("ANDROID_APP_MAIN_PROJECT", "app")
final String MENGINE_APP_DEPLOY_PATH = Utils.getStringProperty("MENGINE_APP_DEPLOY_PATH", rootProject.projectDir.getAbsolutePath() + "/$ANDROID_APP_MAIN_PROJECT")
final Integer MENGINE_APP_BUILD_ASSERTION_DEBUG = Utils.getIntegerProperty("MENGINE_APP_BUILD_ASSERTION_DEBUG", -1)
final Integer MENGINE_APP_BUILD_LOGGER_INFO = Utils.getIntegerProperty("MENGINE_APP_BUILD_LOGGER_INFO", -1)
final Integer MENGINE_APP_BUILD_DEBUG_FILE_PATH = Utils.getIntegerProperty("MENGINE_APP_BUILD_DEBUG_FILE_PATH", -1)
final Integer MENGINE_APP_BUILD_DEBUG_DOCUMENT = Utils.getIntegerProperty("MENGINE_APP_BUILD_DEBUG_DOCUMENT", -1)
final Boolean MENGINE_APP_ENABLE_STRICT_MODE = Utils.getBooleanProperty("MENGINE_APP_ENABLE_STRICT_MODE", false)

final String ANDROID_APP_SCREEN_ORIENTATION = Utils.getStringProperty("ANDROID_APP_SCREEN_ORIENTATION", "sensorPortrait")
final Boolean ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN = Utils.getBooleanProperty("ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN", false)
final Boolean ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER = Utils.getBooleanProperty("ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER", false)

final Boolean MENGINE_APP_LIBRARY_OPENAL32 = Utils.existAppLibrary("MENGINE_APP_LIBRARY_OPENAL32")

println("MENGINE_APP_SECURE_VALUE: $MENGINE_APP_SECURE_VALUE")
println("MENGINE_APP_DEPLOY_PATH: $MENGINE_APP_DEPLOY_PATH")

println("MENGINE_APP_BUILD_ASSERTION_DEBUG: $MENGINE_APP_BUILD_ASSERTION_DEBUG")
println("MENGINE_APP_BUILD_LOGGER_INFO: $MENGINE_APP_BUILD_LOGGER_INFO")
println("MENGINE_APP_BUILD_DEBUG_FILE_PATH: $MENGINE_APP_BUILD_DEBUG_FILE_PATH")
println("MENGINE_APP_BUILD_DEBUG_DOCUMENT: $MENGINE_APP_BUILD_DEBUG_DOCUMENT")

println("MENGINE_APP_ENABLE_STRICT_MODE: $MENGINE_APP_ENABLE_STRICT_MODE")

android {
    externalNativeBuild {
        cmake {
            path "../../../cmake/Android/Mengine/CMakeLists.txt"
            buildStagingDirectory rootProject.projectDir.getAbsolutePath() + "/../outputs/gradle/.cxx/" + project.getName()
        }
    }

    defaultConfig {
        manifestPlaceholders = [
            ANDROID_APP_BUILD_NUMBER                          : ANDROID_APP_BUILD_NUMBER,
            ANDROID_APP_BUILD_VERSION                         : ANDROID_APP_BUILD_VERSION,
            ANDROID_APP_SCREEN_ORIENTATION                    : ANDROID_APP_SCREEN_ORIENTATION,
            ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN         : ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN,
            ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER: ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER
        ]

        println "ANDROID_APP_SCREEN_ORIENTATION: $ANDROID_APP_SCREEN_ORIENTATION"
        println "ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN: $ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN"
        println "ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER: $ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER"

        externalNativeBuild {
            cmake {
                arguments.add("-DANDROID_PLATFORM=android-" + project.minSdkVersion)
                arguments.add("-DANDROID_ARM_NEON=TRUE")
                arguments.add("-DANDROID_STL=c++_shared")
                arguments.add("-DANDROID_TOOLCHAIN=clang")
                arguments.add("-DCMAKE_SYSTEM_NAME=Android")
                arguments.add("-DCMAKE_SYSTEM_VERSION=${project.minSdkVersion}")
                arguments.add("-DMENGINE_BUILD_NUMBER:STRING=${ANDROID_APP_BUILD_NUMBER}")
                arguments.add("-DMENGINE_BUILD_VERSION:STRING=${ANDROID_APP_BUILD_VERSION}")
                arguments.add("-DMENGINE_DEPENDENCIES_PROJECT:STRING=Depends_Android")
                arguments.add("-DMENGINE_BUILD_MENGINE_BUILD_PUBLISH:BOOLEAN=" + (ANDROID_APP_BUILD_PUBLISH ? "ON" : "OFF"))
                arguments.add("-DMENGINE_SECURE_VALUE:STRING=${MENGINE_APP_SECURE_VALUE}")
                arguments.add("-DMENGINE_DEPLOY_PATH:STRING=${MENGINE_APP_DEPLOY_PATH}")

                if (MENGINE_APP_BUILD_ASSERTION_DEBUG != -1) {
                    cppFlags.add("-DMENGINE_ASSERTION_DEBUG=${MENGINE_APP_BUILD_ASSERTION_DEBUG}")
                }

                if (MENGINE_APP_BUILD_LOGGER_INFO != -1) {
                    cppFlags.add("-DMENGINE_LOGGER_INFO=${MENGINE_APP_BUILD_LOGGER_INFO}")
                }

                if (MENGINE_APP_BUILD_DEBUG_FILE_PATH != -1) {
                    cppFlags.add("-DMENGINE_DEBUG_FILE_PATH=${MENGINE_APP_BUILD_DEBUG_FILE_PATH}")
                }

                if (MENGINE_APP_BUILD_DEBUG_DOCUMENT != -1) {
                    cppFlags.add("-DMENGINE_DOCUMENT=${MENGINE_APP_BUILD_DEBUG_DOCUMENT}")
                }
            }
        }
    }

    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    arguments.add("-DCMAKE_BUILD_TYPE:STRING=Debug")
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    arguments.add("-DCMAKE_BUILD_TYPE:STRING=Release")
                }
            }
        }
    }

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/libclang_rt.asan*.so"
            }
        }
    }

    if (MENGINE_APP_LIBRARY_OPENAL32 == true) {
        preBuild.dependsOn ":libraries:OpenAL32:build"
    }

    namespace "org.Mengine.Base"
}

dependencies {
    if (MENGINE_APP_LIBRARY_OPENAL32 == true) {
        implementation project(':libraries:OpenAL32')
    }
}

android {
    defaultConfig {
        buildConfigField "boolean", "MENGINE_APP_ENABLE_STRICT_MODE", "${MENGINE_APP_ENABLE_STRICT_MODE}"

        buildConfigField "boolean", "ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN", "${ANDROID_APP_REQUIRED_HARDWARE_TOUCHSCREEN}"
        buildConfigField "boolean", "ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER", "${ANDROID_APP_REQUIRED_HARDWARE_SENSOR_ACCELEROMETER}"
    }
}