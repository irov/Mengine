import com.Mengine.Utils

def ANDROID_APP_SPLIT_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false)
def ANDROID_APP_BUILD_NUMBER = Utils.getIntegerProperty(project, "ANDROID_APP_BUILD_NUMBER", 0)
def ANDROID_APP_BUILD_PUBLISH = Utils.getBooleanProperty(project, "ANDROID_APP_BUILD_PUBLISH", false)
def ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE = Utils.getBooleanProperty(project, "ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE", false)

apply plugin: 'com.android.library'

android {
    ndkVersion project.ndkVersion
    buildToolsVersion project.buildToolsVersion
    compileSdkVersion project.compileSdkVersion

    compileOptions {
        sourceCompatibility project.javaVersion
        targetCompatibility project.javaVersion
    }

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        multiDexEnabled true

        if (ANDROID_APP_SPLIT_ENABLE == false) {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            }
        }
    }

    splits {
        abi {
            enable ANDROID_APP_SPLIT_ENABLE

            reset()

            include "arm64-v8a", "armeabi-v7a", "x86_64"

            universalApk false
        }
    }

    externalNativeBuild {
        cmake {
            version project.cmakeVersion

            path "../../../cmake/Android_SDL/CMakeLists.txt"
        }
    }

    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-" + project.minSdkVersion,
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DANDROID_ALLOW_UNDEFINED_SYMBOLS:BOOLEAN=TRUE",
                            "-DMENGINE_BUILD_VERSION:STRING=" + ANDROID_APP_BUILD_NUMBER,
                            "-DMENGINE_BUILD_MENGINE_BUILD_PUBLISH=" + (ANDROID_APP_BUILD_PUBLISH ? "ON" : "OFF"),
                            "-DMENGINE_BUILD_MENGINE_LOGGER_LEVEL_FORCE_VERBOSE=" + (ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE ? "ON" : "OFF"),
                            "-DMENGINE_BUILD_MENGINE_MASTER_RELEASE=OFF",
                            "-DMENGINE_BUILD_MENGINE_DEVELOPMENT=ON",
                            "-DCMAKE_BUILD_TYPE:STRING=Debug"
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PLATFORM=android-" + project.minSdkVersion,
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DANDROID_ALLOW_UNDEFINED_SYMBOLS:BOOLEAN=TRUE",
                            "-DMENGINE_BUILD_VERSION:STRING=" + ANDROID_APP_BUILD_NUMBER,
                            "-DMENGINE_BUILD_MENGINE_BUILD_PUBLISH=" + (ANDROID_APP_BUILD_PUBLISH ? "ON" : "OFF"),
                            "-DMENGINE_BUILD_MENGINE_LOGGER_LEVEL_FORCE_VERBOSE=" + (ANDROID_APP_LOGGER_LEVEL_FORCE_VERBOSE ? "ON" : "OFF"),
                            "-DMENGINE_BUILD_MENGINE_MASTER_RELEASE=ON",
                            "-DMENGINE_BUILD_MENGINE_DEVELOPMENT=OFF",
                            "-DCMAKE_BUILD_TYPE:STRING=Release"
                }
            }
        }
    }

    preBuild.dependsOn ":libraries:SDL2:build", ":libraries:OpenAL32:build"
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.preference:preference:1.2.0"

    implementation "androidx.lifecycle:lifecycle-process:2.5.1"
    implementation "androidx.lifecycle:lifecycle-common:2.5.1"

    implementation project(':libraries:SDL2')
    implementation project(':libraries:OpenAL32')
}
