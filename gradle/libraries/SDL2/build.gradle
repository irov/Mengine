import com.Mengine.Utils

def ANDROID_APP_SPLIT_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false)

apply plugin: 'com.android.library'

android {
    ndkVersion project.ndkVersion
    buildToolsVersion project.buildToolsVersion
    compileSdkVersion project.compileSdkVersion

    compileOptions {
        sourceCompatibility project.javaVersion
        targetCompatibility project.javaVersion
    }

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        multiDexEnabled true

        if (ANDROID_APP_SPLIT_ENABLE == false) {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            }
        }
    }

    splits {
        abi {
            enable ANDROID_APP_SPLIT_ENABLE

            reset() //reset the list of ABIs to be included to an empty string

            include "arm64-v8a", "armeabi-v7a", "x86_64"

            universalApk false
        }
    }

    externalNativeBuild {
        cmake {
            version project.cmakeVersion
        }
    }

    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    cFlags "-std=c11 -O0 -g -D_DEBUG"
                    cppFlags "-std=c++17 -frtti -fexceptions -O0 -g -D_DEBUG"
                    arguments "-DANDROID_PLATFORM=android-" + project.minSdkVersion,
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DCMAKE_BUILD_TYPE:STRING=Debug",
                            "-DSDL_SHARED:BOOLEAN=ON",
                            "-DSDL_STATIC:BOOLEAN=OFF",
                            "-DSDL_JOYSTICK:BOOLEAN=ON",
                            "-DSDL_JOYSTICK_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_HAPTIC:BOOLEAN=ON",
                            "-DSDL_HAPTIC_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_POWER:BOOLEAN=ON",
                            "-DSDL_POWER_ENABLED_BY_DEFAULT:BOOLEAN=ON"
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    cFlags "-std=c11 -O3 -DNDEBUG"
                    cppFlags "-std=c++17 -frtti -fexceptions -O3 -DNDEBUG"
                    arguments "-DANDROID_PLATFORM=android-" + project.minSdkVersion,
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DCMAKE_BUILD_TYPE:STRING=Release",
                            "-DSDL_SHARED:BOOLEAN=ON",
                            "-DSDL_STATIC:BOOLEAN=OFF",
                            "-DSDL_JOYSTICK:BOOLEAN=ON",
                            "-DSDL_JOYSTICK_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_HAPTIC:BOOLEAN=ON",
                            "-DSDL_HAPTIC_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_POWER:BOOLEAN=ON",
                            "-DSDL_POWER_ENABLED_BY_DEFAULT:BOOLEAN=ON"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "../../../dependencies/SDL2/CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = ['../../../dependencies/SDL2/android-project/app/src/main/java/']
            }
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
