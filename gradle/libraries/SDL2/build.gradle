apply plugin: 'com.android.library'

import com.Mengine.Utils

android {
    ndkVersion '22.0.7026061'
    buildToolsVersion '31.0.0'
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 32

        if (Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false) == false) {
            ndk {
                if (Utils.getBooleanProperty(project, "ANDROID_APP_ARCH_86_64_ENABLE", true) == true) {
                    abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
                } else {
                    abiFilters "arm64-v8a", "armeabi-v7a"
                }
            }
        }
    }

    splits {
        abi {
            if (Utils.getBooleanProperty(project, "ANDROID_APP_SPLIT_ENABLE", false) == true) {
                enable true
            } else {
                enable false
            }

            reset() //reset the list of ABIs to be included to an empty string

            if (Utils.getBooleanProperty(project, "ANDROID_APP_ARCH_86_64_ENABLE", true) == true) {
                include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            } else {
                include "arm64-v8a", "armeabi-v7a"
            }

            universalApk false
        }
    }

    externalNativeBuild {
        cmake {
            version "3.10.2.4988404"
        }
    }

    buildTypes {
        debug {

            externalNativeBuild {
                cmake {
                    cFlags "-std=c11 -g -D_DEBUG"
                    cppFlags "-std=c++17 -frtti -fexceptions -g -D_DEBUG"
                    arguments "-DANDROID_PLATFORM=android-19",
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DCMAKE_BUILD_TYPE:STRING=Debug",
                            "-DSDL_SHARED:BOOLEAN=ON",
                            "-DSDL_STATIC:BOOLEAN=OFF",
                            "-DSDL_JOYSTICK:BOOLEAN=ON",
                            "-DSDL_JOYSTICK_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_HAPTIC:BOOLEAN=ON",
                            "-DSDL_HAPTIC_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_POWER:BOOLEAN=ON",
                            "-DSDL_POWER_ENABLED_BY_DEFAULT:BOOLEAN=ON"
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    cFlags "-std=c11 -O3 -DNDEBUG"
                    cppFlags "-std=c++17 -frtti -fexceptions -O3 -DNDEBUG"
                    arguments "-DANDROID_PLATFORM=android-19",
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_STL=c++_shared",
                            "-DANDROID_TOOLCHAIN=clang",
                            "-DCMAKE_BUILD_TYPE:STRING=Release",
                            "-DSDL_SHARED:BOOLEAN=ON",
                            "-DSDL_STATIC:BOOLEAN=OFF",
                            "-DSDL_JOYSTICK:BOOLEAN=ON",
                            "-DSDL_JOYSTICK_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_HAPTIC:BOOLEAN=ON",
                            "-DSDL_HAPTIC_ENABLED_BY_DEFAULT:BOOLEAN=ON",
                            "-DSDL_POWER:BOOLEAN=ON",
                            "-DSDL_POWER_ENABLED_BY_DEFAULT:BOOLEAN=ON"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "../../../dependencies/SDL2/CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            java {
                srcDirs = ['../../../dependencies/SDL2/android-project/app/src/main/java/']
            }
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
