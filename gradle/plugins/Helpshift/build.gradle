import com.Mengine.Utils

def ANDROID_APP_DEBUG_MINIFY_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_DEBUG_MINIFY_ENABLE", false)
def ANDROID_APP_RELEASE_MINIFY_ENABLE = Utils.getBooleanProperty(project, "ANDROID_APP_RELEASE_MINIFY_ENABLE", false)

def MENGINE_APP_PLUGIN_FIREBASE_MESSAGING = Utils.existAppPlugin(project, "MENGINE_APP_PLUGIN_FIREBASE_MESSAGING")

apply plugin: 'com.android.library'

android {
    ext.extensions = []

    buildToolsVersion project.buildToolsVersion
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility project.javaVersion
        targetCompatibility project.javaVersion
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        main {
            if (MENGINE_APP_PLUGIN_FIREBASE_MESSAGING == true) {
                println "Plugin [Helpshift] add extension 'firebaseMessaging'"
                
                java.srcDirs += ['firebaseMessaging']
            }
            
            if (project.hasProperty("ANDROID_APP_ASSETS_RES_DIR")) {
                res.srcDirs += [ANDROID_APP_ASSETS_RES_DIR]
            } else {
                res.srcDirs += ['../../app/res']
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled ANDROID_APP_DEBUG_MINIFY_ENABLE

            consumerProguardFiles 'proguard-rules.pro'
        }

        release {
            minifyEnabled ANDROID_APP_RELEASE_MINIFY_ENABLE

            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

if (MENGINE_APP_PLUGIN_FIREBASE_MESSAGING == true) {
    android.ext.extensions += 'org.Mengine.Plugin.Helpshift.MengineHelpshiftExtensionFirebaseMessaging'

    dependencies {
        implementation project(path: ':plugins:FirebaseMessaging')

        implementation 'com.google.firebase:firebase-messaging'
    }
}

dependencies {
    implementation project(':libraries:Mengine')
    implementation project(':libraries:SDL2')

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.helpshift:helpshift-sdkx:10.2.0'
}

android {
    defaultConfig {
        buildConfigField "String[]", "MENGINE_GRADLE_ANDROID_PLUGIN_EXTENSIONS", '{' + android.ext.extensions.collect {
            "\"${it}\""
        }.join(",") + '}'
    }
}